plugins {
    id "org.jetbrains.intellij" version "0.4.21"
}
apply plugin: 'groovy'

intellij {
    version '202.5103-EAP-CANDIDATE-SNAPSHOT'
    pluginName 'jetbrains-source-plugin'
    plugins = ["java", "Groovy", "Kotlin", "properties", "gradle"]
    buildSearchableOptions.enabled = false
}
patchPluginXml {
    sinceBuild = "201"
}
publishPlugin {
    token = System.getenv("JETBRAINS_PUBLISH_PLUGIN_TOKEN")
}

group = 'com.sourceplusplus.plugin'
archivesBaseName = 'jetbrains-source-plugin'

dependencies {
    implementation project(":api")
    compile project(path: ":core", configuration: 'shadow')
    implementation(project(":portal")) {
        exclude group: 'com.jetbrains.intellij.platform'
    }
    implementation 'org.codehaus.groovy:groovy-all:2.5.11'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
//    implementation "io.vertx:vertx-web:$VERTX_VERSION"
    implementation files('../../3rd_party/vertx/vertx-web-3.9.1.jar')
    implementation "io.vertx:vertx-tcp-eventbus-bridge:$VERTX_VERSION"
    implementation('com.github.sourceplusplus.sourcemarker:marker-provider:8fe90860ef') {
        transitive = false
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/source-plugin_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['ide'] = 'jetbrains'
            p['release'] = "alpha"
            p['full_version'] = p['version'] + "-" + p['release']
            p['apache_skywalking_version'] = APACHE_SKYWALKING_VERSION
            p['apache_skywalking_full_version'] = APACHE_SKYWALKING_VERSION + "-es6"
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

processResources {
    def portalResources = file('../../portal/src/main/resources')
    if (!portalResources.exists()) throw new IllegalStateException("Could not find portal resources")
    from(portalResources)

    def coreResources = file('../../core/src/main/resources')
    if (!coreResources.exists()) throw new IllegalStateException("Could not find core resources")
    from(coreResources)

    def apmTar = file('../../3rd_party/skywalking/apache-skywalking-apm-8.0.1.tar.gz')
    if (!apmTar.exists()) throw new IllegalStateException("Could not find APM tar")
    from(apmTar) { into('skywalking') }

    def customizeEnhance = file('../../3rd_party/skywalking/apm-customize-enhance-plugin-8.0.1.jar')
    if (!customizeEnhance.exists()) throw new IllegalStateException("Could not find APM customize enhance jar")
    from(customizeEnhance) { into('skywalking') }

    def vertxPlugin = file('../../3rd_party/skywalking/apm-vertx-core-3.x-plugin-8.0.1.jar')
    if (!vertxPlugin.exists()) throw new IllegalStateException("Could not find APM vert.x plugin jar")
    from(vertxPlugin) { into('skywalking') }
}