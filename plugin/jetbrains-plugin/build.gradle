plugins {
    id "org.jetbrains.intellij" version "0.4.16"
}

apply plugin: 'groovy'
intellij {
    version '201.6073-EAP-CANDIDATE-SNAPSHOT'
    updateSinceUntilBuild = false
    pluginName 'jetbrains-source-plugin'
    plugins = ["java", "Groovy", "Kotlin", "properties", "gradle"]
    buildSearchableOptions.enabled = false
}
runIde {
    systemProperty('ide.browser.jcef.enabled', 'true')
    //systemProperty('idea.auto.reload.plugins', 'true') //todo: remove at 0.4.17
}

group = 'com.sourceplusplus.plugin'
archivesBaseName = 'jetbrains-source-plugin'

dependencies {
    implementation project(":api")
    implementation(project(":portal")) {
        exclude group: 'com.jetbrains.intellij.platform'
    }
    implementation 'org.codehaus.groovy:groovy-all:2.4.17'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
    implementation "io.vertx:vertx-web:$VERTX_VERSION"
    implementation "io.vertx:vertx-tcp-eventbus-bridge:$VERTX_VERSION"
    implementation(group: 'com.github.codebrig', name: 'gitsocratic', version: '5d33d30262') {
        exclude group: 'com.github.sourceplusplus.assistant', module: 'source-api'
    }
    implementation('com.github.sourceplusplus.SourceMarker:marker-provider:0eb756dc17') { //todo: real version
        transitive = false
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/source-plugin_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['ide'] = 'jetbrains'
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

task updatePluginResources(dependsOn: [":agent:jvm-agent:buildAgent"]) {
    doLast {
        copy {
            from '../../portal/src/main/resources'
            into 'build/resources/main'
        }
        file("build/resources/main/source-portal_build.properties").delete()

        copy {
            from '../../agent/lib/skywalking'
            into 'build/resources/main'
        }
        file("build/resources/main/").mkdirs()
        file("build/resources/main/source-agent-$version" + ".jar").delete()
        file("build/resources/main/source-agent-$version" + ".jar") <<
                file("../../agent/jvm-agent/build/libs/source-agent-$version" + ".jar").bytes
    }
}
compileJava.mustRunAfter updatePluginResources
runIde.dependsOn updatePluginResources
buildPlugin.dependsOn updatePluginResources
