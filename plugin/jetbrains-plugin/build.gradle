plugins {
    id "org.jetbrains.intellij" version "0.4.21"
}
apply plugin: 'groovy'

intellij {
    version '202.5103-EAP-CANDIDATE-SNAPSHOT'
    pluginName 'jetbrains-source-plugin'
    plugins = ["java", "Groovy", "Kotlin", "properties", "gradle"]
    buildSearchableOptions.enabled = false
}
patchPluginXml {
    sinceBuild = "201"
}

group = 'com.sourceplusplus.plugin'
archivesBaseName = 'jetbrains-source-plugin'

dependencies {
    implementation project(":api")
    compile project(path: ":core", configuration: 'shadow')
    implementation(project(":portal")) {
        exclude group: 'com.jetbrains.intellij.platform'
    }
    implementation 'org.codehaus.groovy:groovy-all:2.5.11'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
//    implementation "io.vertx:vertx-web:$VERTX_VERSION"
    implementation files('../../3rd_party/vertx/vertx-web-3.9.1.jar')
    implementation "io.vertx:vertx-tcp-eventbus-bridge:$VERTX_VERSION"
    implementation(group: 'com.github.codebrig', name: 'gitsocratic', version: '5097fbbef8') { //todo: real version
        exclude group: 'com.github.sourceplusplus.assistant', module: 'source-api'
        exclude group: 'org.codehaus.groovy'
    }
    implementation('com.github.sourceplusplus.sourcemarker:marker-provider:462531cac7') {
        transitive = false
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/source-plugin_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['ide'] = 'jetbrains'
            p['release'] = "alpha"
            p['full_version'] = p['version'] + "-" + p['release']
            p['apache_skywalking_version'] = APACHE_SKYWALKING_VERSION
            p['apache_skywalking_full_version'] = APACHE_SKYWALKING_VERSION + "-es6"
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

task updatePluginResources {
    doLast {
        copy {
            from '../../portal/src/main/resources'
            into 'build/resources/main'
        }
        file("build/resources/main/source-portal_build.properties").delete()

        copy {
            from '../../core/build/resources/main'
            into 'build/resources/main'
        }

        copy {
            from '../../3rd_party'
            into 'build/resources/main'
        }
    }
}
compileJava.mustRunAfter updatePluginResources
runIde.dependsOn updatePluginResources
buildPlugin.dependsOn updatePluginResources
