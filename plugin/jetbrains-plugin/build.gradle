plugins {
    id "org.jetbrains.intellij" version "0.3.12"
}

apply plugin: 'groovy'
intellij {
    pluginName 'jetbrains-source-plugin'
    plugins = ["Groovy", "Kotlin", "properties"]
}

group = 'com.sourceplusplus.plugin'
archivesBaseName = 'jetbrains-source-plugin'

dependencies {
    compile project(":api")
    compile project(":tooltip")
    compile project(":plugin:source-plugin-commons")
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
    compile group: 'com.googlecode.wicked-charts', name: 'highcharts-wrapper', version: '2.0.1'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/source-plugin_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['ide'] = 'jetbrains'
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

task updatePluginResources(dependsOn: [":agent:jvm-agent:buildAgent"]) {
    doLast {
        copy {
            from '../../tooltip/src/main/resources'
            into 'build/resources/main'
        }
        file("build/resources/main/source-tooltip_build.properties").delete()

        copy {
            from '../../agent/lib/skywalking'
            into 'build/resources/main'
        }
        file("build/resources/main/").mkdirs()
        file("build/resources/main/source-agent-$version" + ".jar").delete()
        file("build/resources/main/source-agent-$version" + ".jar") <<
                file("../../agent/jvm-agent/build/libs/source-agent-$version" + ".jar").bytes
    }
}
compileJava.mustRunAfter updatePluginResources
runIde.dependsOn updatePluginResources
buildPlugin.dependsOn updatePluginResources
//todo: doesn't build api 49mb vs 50.01mb
