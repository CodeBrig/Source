plugins {
    id "org.jetbrains.intellij" version "0.4.17"
}

apply plugin: 'groovy'
intellij {
    version '2019.3.4'
    updateSinceUntilBuild = false
    pluginName 'jetbrains-source-plugin'
    plugins = ["java", "Groovy", "Kotlin", "properties", "gradle"]
}

group = 'com.sourceplusplus.plugin'
archivesBaseName = 'jetbrains-source-plugin'

dependencies {
    compile project(":api")
    compile (project(":portal")) {
        exclude group: 'com.jetbrains.intellij.platform'
    }
    compile project(":plugin:source-plugin-commons")
    compile 'org.codehaus.groovy:groovy-all:2.4.17'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.30'
    compile(group: 'com.github.codebrig', name: 'gitsocratic', version: '5d33d30262') {
        exclude group: 'com.github.sourceplusplus.assistant', module: 'source-api'
    }
    compile 'com.github.codebrig:journey:0.4.0-online'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/source-plugin_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['build_date'] = new Date().toInstant().toString()
            p['ide'] = 'jetbrains'
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}

task updatePluginResources(dependsOn: [":agent:jvm-agent:buildAgent"]) {
    doLast {
        copy {
            from '../../portal/src/main/resources'
            into 'build/resources/main'
        }
        file("build/resources/main/source-portal_build.properties").delete()

        copy {
            from '../../agent/lib/skywalking'
            into 'build/resources/main'
        }
        file("build/resources/main/").mkdirs()
        file("build/resources/main/source-agent-$version" + ".jar").delete()
        file("build/resources/main/source-agent-$version" + ".jar") <<
                file("../../agent/jvm-agent/build/libs/source-agent-$version" + ".jar").bytes
    }
}
compileJava.mustRunAfter updatePluginResources
runIde.dependsOn updatePluginResources
buildPlugin.dependsOn updatePluginResources
