apply plugin: 'application'
apply plugin: 'groovy'

group = 'com.sourceplusplus.core'
applicationName = 'source-core'
archivesBaseName = 'source-core'
mainClassName = 'com.sourceplusplus.core.CoreBootstrap'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    compile project(":api")
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.jetbrains', name: 'annotations', version: '13.0'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: '2.9.2'
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.2.5'

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.1'
    compile group: 'io.vertx', name: 'vertx-dropwizard-metrics', version: '3.6.3'
    compile group: 'io.vertx', name: 'vertx-web-client', version: '3.6.3'
    compile 'io.vertx:vertx-auth-jwt:3.6.3'
    compile 'io.vertx:vertx-web:3.6.3'
    compile 'io.vertx:vertx-tcp-eventbus-bridge:3.6.3'
    compile group: 'io.searchbox', name: 'jest', version: '6.3.1'
    compile group: 'net.jodah', name: 'expiringmap', version: '0.5.9'
}

configurations {
    compile.exclude module: 'jackson-module-scala_2.10'
    compile.exclude module: 'log4j-over-slf4j'
}

test {
    dependsOn "buildAndMove", "startDocker"
    finalizedBy "stopDocker"
}

task runCore {
    dependsOn "startDocker"

    doLast {
        file("docker/source-core-" + version + ".zip").delete()
        file("../docker/source-core-" + version + ".zip").delete()
    }
}

task buildAndMove {
    dependsOn ":core:distZip"
    doLast {
        file("core/build/distributions/source-core-" + version + ".zip").renameTo(file("docker/source-core-" + version + ".zip"))
        file("../core/build/distributions/source-core-" + version + ".zip").renameTo(file("../docker/source-core-" + version + ".zip"))
    }
}

task startDocker {
    dependsOn ":composeUp"
    doFirst {
        file("docker/source-core-" + version + ".zip").delete()
        file("../docker/source-core-" + version + ".zip").delete()
    }
}
startDocker.mustRunAfter buildAndMove

task stopDocker {
    dependsOn ":composeDown"
    doLast {
        file("docker/source-core-" + version + ".zip").delete()
        file("../docker/source-core-" + version + ".zip").delete()
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/source-core_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['apache_skywalking_version'] = project.APACHE_SKYWALKING_VERSION.toString()
            p['build_date'] = new Date().toInstant().toString()
            p.store w, null
        }
    }
}
classes {
    dependsOn createProperties
}
